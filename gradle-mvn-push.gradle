/**
 * Copyright 2020 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven-publish'
apply plugin: 'signing'

archivesBaseName = 'android-maps-utils'
group = 'com.google.maps.android'

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        aar(MavenPublication) {
            pom {
                name = 'Google Maps Android API utility library'
                description = 'Handy extensions to the Google Maps Android API.'
                url = 'https://github.com/googlemaps/android-maps-utils'

                scm {
                    url = 'scm:git@github.com:googlemaps/android-maps-utils.git'
                    connection = 'scm:git@github.com:googlemaps/android-maps-utils.git'
                    developerConnection = 'scm:git@github.com:googlemaps/android-maps-utils.git'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                organization {
                    name = 'Google Inc'
                    url = 'http://developers.google.com/maps'
                }

                developers {
                    developer {
                        id = 'broady'
                        name = 'Chris Broadfoot'
                        url = 'http://google.com/+ChristopherBroadfoot'
                    }
                }
            }
            groupId group
            artifactId 'android-maps-utils'
            version '1.0.2'
            afterEvaluate {
                artifact bundleReleaseAar
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.aar
}
