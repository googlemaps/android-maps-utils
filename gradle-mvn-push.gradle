apply plugin: 'maven-publish'
apply plugin: 'signing'

archivesBaseName = 'android-maps-utils'
group = 'com.google.maps.android'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

publishing {
    publications {
        aar(MavenPublication) {
            pom {
                name = 'Google Maps Android API utility library'
                description = 'Handy extensions to the Google Maps Android API.'
                url = 'https://github.com/googlemaps/android-maps-utils'

                scm {
                    url = 'scm:git@github.com:googlemaps/android-maps-utils.git'
                    connection = 'scm:git@github.com:googlemaps/android-maps-utils.git'
                    developerConnection = 'scm:git@github.com:googlemaps/android-maps-utils.git'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                organization {
                    name = 'Google Inc'
                    url = 'http://developers.google.com/maps'
                }

                developers {
                    developer {
                        id = 'broady'
                        name = 'Chris Broadfoot'
                        url = 'http://google.com/+ChristopherBroadfoot'
                    }
                }
            }
            groupId group
            artifactId 'android-maps-utils'
            version '0.6.1'
            afterEvaluate {
                artifact bundleReleaseAar
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.aar
}
