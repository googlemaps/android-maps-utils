import org.apache.tools.ant.filters.ConcatFilter

/**
 * Copyright 2020 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.application'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'kotlin-android'
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.google.maps.android.utils.demo"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "version"
    productFlavors {
        gms {
            dimension "version"
            applicationIdSuffix ".gms"
            versionNameSuffix "-gms"
        }
        v3 {
            dimension "version"
            applicationIdSuffix ".v3"
            versionNameSuffix "-v3"
            multiDexEnabled true
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

// [START maps_android_utils_install_snippet]
dependencies {

    // [START_EXCLUDE silent]
    // implementation project(':library')
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // GMS
    gmsImplementation 'com.google.android.gms:play-services-maps:17.0.1'
    gmsImplementation 'com.google.maps.android:android-maps-utils:2.2.5'

    // V3
    v3Implementation 'com.google.android.libraries.maps:maps:3.1.0-beta'
    v3Implementation 'com.android.volley:volley:1.2.0' // TODO - Remove this after Maps SDK v3 beta includes Volley versions on Maven Central
    v3Implementation 'com.google.maps.android:android-maps-utils-v3:2.2.5'
    v3Implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    // [END_EXCLUDE]
}
// [END maps_android_utils_install_snippet]

task generateV3(type: Copy) {
    group "V3 Beta"
    description "Copies source code from GMS to V3 BETA."

    from 'src/gms/java'
    into 'src/v3/java'
    filter { line ->
        line.replace('com.google.android.gms.maps', 'com.google.android.libraries.maps')
    }
    filter(ConcatFilter, prepend: file('V3_FILE_HEADER'))
}

task generateV3Layout(type: Copy) {
    group "V3 Beta"
    description "Copies layout files from GMS to V3 BETA."

    from 'src/gms/res/layout'
    into 'src/v3/res/layout'
    filter { line ->
        line.replace('com.google.android.gms.maps', 'com.google.android.libraries.maps')
    }
}

task startGmsDemo(type: Exec) {
    dependsOn 'installGmsDebug'
    def adb = new File("${System.env.ANDROID_HOME}", "platform-tools${File.separator}adb")
    commandLine adb, "shell", "am", "start", "-n", "com.google.maps.android.utils.demo.gms/com.google.maps.android.utils.demo.MainActivity"
}

task startV3Demo(type: Exec) {
    dependsOn 'installV3Debug'
    def adb = new File("${System.env.ANDROID_HOME}", "platform-tools${File.separator}adb")
    commandLine adb, "shell", "am", "start", "-n", "com.google.maps.android.utils.demo.v3/com.google.maps.android.utils.demo.MainActivity"
}

secrets {
    // To add your Maps API key to this project:
    // 1. Open the file local.properties found under the root project
    // 2. Add this line, where YOUR_API_KEY is your API key:
    //        MAPS_API_KEY=YOUR_API_KEY
    defaultPropertiesFileName 'local.defaults.properties'
}
