buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

apply plugin: 'android-library'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = 'android-maps-utils'
group = 'com.google.maps.android'

android {
    compileSdkVersion 19
    buildToolsVersion "21.1.2"
    defaultPublishConfig "release"
    publishNonDefault true

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
        }

        gms{
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }

        m4b {
            java.srcDirs = ['m4b/src']
            res.srcDirs = ['m4b/res']
        }

        androidTest {
            java.srcDirs = ['tests/src']
        }
    }

    productFlavors {
        gms {
            dependencies {
                compile 'com.google.android.gms:play-services-maps:6.5.87'
            }
        }

        m4b {
            dependencies {
                compile files('../maps_m4b.jar')
            }
        }
    }
}

task buildM4b (dependsOn: build)

gradle.taskGraph.whenReady { TaskExecutionGraph graph ->
    def isM4bEnabled = graph.hasTask(buildM4b)
    for (task in graph.allTasks) {
        if (task.name.indexOf("M4b") > 0) {
            task.enabled = isM4bEnabled
        }
    }
}

task genM4bSrc(type: Copy) {
    from('src'){
        include '**/*.java'
        filter({
            line -> line.replaceAll("com.google.android.gms.maps",
                    "com.google.android.m4b.maps")
        })
    }
    from('src'){
        exclude '**/*.java'
    }
    into 'm4b/src'
}

task genM4bRes(type: Copy) {
    from('res'){
        include '**/*.xml'
        filter({
            line -> line.replaceAll("com.google.android.gms.maps",
                    "com.google.android.m4b.maps")
        })
    }
    from('res'){
        exclude '**/*.xml'
    }
    into 'm4b/res'
}

preBuild.dependsOn genM4bSrc, genM4bRes

task instrumentTest(dependsOn: connectedCheck)

task apklib(type: Zip) {
    dependsOn 'packageGmsReleaseJar'
    appendix = extension = 'apklib'

    from 'AndroidManifest.xml'
    into('res') {
        from 'res'
    }
    into('src') {
        from 'src'
    }
}

artifacts {
    archives apklib
}

signing {
    sign configurations.archives
}

if(!hasProperty("sonatypeUsername")){
    sonatypeUsername = ""
}
if(!hasProperty("sonatypePassword")){
    sonatypePassword = ""
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            modifyPom(addFilter('aar') { artifact, file ->
                artifact.name == 'android-maps-utils'
            })

            modifyPom(addFilter('apklib') { artifact, file ->
                artifact.name == 'android-maps-utils-apklib'
            })

            // There's no official apklib for Google Play services, so we
            // can't really maintain that dependency for the apklib packaging.
            pom('apklib').whenConfigured { pom ->
                pom.dependencies = []
            }
        }
    }
}

def modifyPom(pom) {
    pom.project {
        name 'Google Maps Android API utility library'
        description 'Handy extensions to the Google Maps Android API.'
        url 'https://github.com/googlemaps/android-maps-utils'

        scm {
            url 'scm:git@github.com:googlemaps/android-maps-utils.git'
            connection 'scm:git@github.com:googlemaps/android-maps-utils.git'
            developerConnection 'scm:git@github.com:googlemaps/android-maps-utils.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        organization {
            name 'Google Inc'
            url 'http://developers.google.com/maps'
        }

        developers {
            developer {
                id 'broady'
                name 'Chris Broadfoot'
                url 'http://google.com/+ChristopherBroadfoot'
            }
        }
    }
}
