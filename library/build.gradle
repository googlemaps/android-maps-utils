/**
 * Copyright 2023 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
}

dokkaHtml {
    moduleName.set("android-maps-utils ")
}

android {

    defaultConfig {
        compileSdk 34
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        consumerProguardFiles 'consumer-rules.pro'
        // This enables us to tell when we're running unit tests on Travis (#573)
        buildConfigField("String", "TRAVIS", "\"" + System.getenv('TRAVIS') + "\"")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    resourcePrefix 'amu_'
    // This enables long timeouts required on slow environments, e.g. Travis
    adbOptions {
        timeOutInMs 10 * 60 * 1000  // 10 minutes
        installOptions "-d", "-t"
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    testOptions {
        animationsDisabled true
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
    namespace 'com.google.maps.android'
}

dependencies {
    // We are adding api() for the Maps SDK for Android, so it propagates to the app-level modules.
    api  'com.google.android.gms:play-services-maps:18.2.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.13.1'
    lintPublish project(':lint-checks')
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.12.1'
    testImplementation 'net.sf.kxml:kxml2:2.3.0'
    testImplementation "io.mockk:mockk:1.13.6"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

task instrumentTest(dependsOn: connectedCheck)

if (System.getenv("JITPACK")) {
    apply plugin: 'maven'
}
